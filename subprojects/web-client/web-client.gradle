plugins {
    id "com.moowork.node" version "0.11"
}

apply plugin:'java'

node {
    version = '4.2.2'
    npmVersion = '3.4.1'
    download = true
}

ext.jsDistributionDir = 'dist'

jar {
	from(fileTree(jsDistributionDir)) {
		into 'META-INF/resources'
	}
}

def githubAuthToken = "$System.env.JSPM_GITHUB_AUTH_TOKEN"

// Avoid Github rate limiting on CI instances
// See https://github.com/jspm/jspm-cli/wiki/Registries#travis-ci
task jspm_config(type: NpmTask, dependsOn: npm_install) {
    args = ['run', 'jspm-config', '--', "$githubAuthToken"]
}

task jspm_install(type: NpmTask, dependsOn: npm_install) {
    inputs.file file('package.json')
    outputs.dir file('src/lib')

    args = ['run', 'jspm-install']
}

task npm_build(type: NpmTask, dependsOn: jspm_install) {
    inputs.dir file('src')
    outputs.dir file(jsDistributionDir)
    args = ['run', 'build']
}

task npm_clean(type: NpmTask) {
    args = ['run', 'clean']
}

if (githubAuthToken != "null") {
    jspm_install.setDependsOn([jspm_config])
}

jar.dependsOn npm_build

clean.dependsOn npm_clean